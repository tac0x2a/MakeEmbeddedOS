OUTPUT_FORMAT("elf32-h8300")
OUTPUT_ARCH(h8300h)
ENTRY("_start")

MEMORY
{
		romall(rx) : o = 0x000000, l = 0x080000 /* 512KB,ROM全体 */
		vectors(r) : o = 0x000000, l = 0x000100
		rom(rx)    : o = 0x000100, l = 0x07ff00

		ramall(rwx): o = 0xffbf20, l = 0x004000 /* 16KB */
		data(rwx)  : o = 0xfffc20, l = 0x000300 /* 16KB */
		stack(rw)  : o = 0xffff00, l = 0x000000 /* スタックのデフォルトサイズは0 */
}

/* メモリマップ */
SECTIONS
{
				/* 割り込みベクタ */
				.vectors : {
							vector.o(.data)
				} > vectors

				/* テキスト領域
				 * コンパイル後の機械語コードが配置される
				 */
				.text : {
							_text_start = . ;
							*(.text)
							_etext = . ;
				} > rom

				/* read-only データ */
				.rodata : {
								_rodata_start = . ;
								*(.strings)
								*(.rodata)
								*(.rodata.*)
								_erodata = . ;
				} > rom

				/* データ領域
				 * 変数などが配置される
				 */
				.data : {
							_data_start = . ;
							*(.data)
							_edata = . ;
				} > data AT> rom

				/* BSS領域 */
				.bss : {
						 _bss_start = . ;
						 *(.bss)
						 *(COMMON)
						 _ebss = . ;
				} > data AT> rom

				.  = ALIGN(4); /* ロケーションカウンタを4バイト境界にする */
				_end = . ; /* セクションの終端の意味 */

				.stack : {
								_stack = .;
				} > stack

} /* end of SECTIONS */
